apply plugin: "com.android.application"

def packageName = "com.sap.sailing.android.tracking.app"
def verCode = 1

android {
    buildToolsVersion "21.1.2"
    compileSdkVersion 21

    defaultConfig {
        applicationId packageName
        minSdkVersion 14
        targetSdkVersion 21
        versionCode verCode
        versionName "$verCode - Altitude"
        useJack false
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ['assets']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix ".debug"

            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/maven/com.googlecode.json-simple/json-simple/pom.properties"
        exclude "META-INF/maven/com.googlecode.json-simple/json-simple/pom.xml"
    }
}

dependencies {
    compile "net.hockeyapp.android:HockeySDK:3.5.0"

    /* support libraries */
    compile "com.android.support:appcompat-v7:21.0.3"

    /* Google Play Services */
    compile 'com.google.android.gms:play-services-location:6.5.87'

    /* local dependencies */
    compile project(":mobile:com.sap.sailing.android.shared")

    /* all local jar-libs */
//    compile fileTree(dir: "libs", include: ["*.jar"])
}

// Custom tasks

/**
 * Deletes apk and version from www apps directory
 */
clean.doLast {
    ant.delete() {
        fileset(dir: file("../../java/com.sap.sailing.www/apps"), includes: "$packageName*")
    }
}

/**
 * Copies apk into www apps directory and create version file
 */
build.doLast {
    // Copy APK file
    copy {
        from ("build/outputs/apk") {
            include "**/*release-unsigned.apk"
        }
        into "../../java/com.sap.sailing.www/apps"
        rename { String fileName -> fileName.replace("-release-unsigned", "") }
    }
//
//    // Create version file
//    xmlFile = file("$wwwFolder/$packageName.version")
}