apply plugin: "com.android.application"
apply plugin: 'com.getkeepsafe.dexcount'
apply from: "../git.gradle"

def packageName="com.sap.sailing.android.tracking.app"
def verCode=1

android {
    buildToolsVersion rootProject.ext.buildTools
    compileSdkVersion rootProject.ext.compileSdk

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    /**
     * Copies apk into www apps directory and create version file
     */
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName = outputFileName.replace(".apk", "-v" + variant.versionName + ".apk");
            newName = newName.replace("-" + variant.buildType.name, "");
            outputFileName = new File(newName);
            def outputDir = "$buildDir/outputs/apk/${output.baseName.replace("-", "/")}/";
            // Create version file
            def versionFile = new File(outputDir + "$packageName." + "version");
            versionFile.text = "$packageName" + ".apk=$verCode";
            def taskName = variant.name.capitalize();
            tasks["assemble$taskName"].finalizedBy = [rootProject.ext.createCopyTask('tracking', outputDir, output)]
        }
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode verCode
        versionName "$verCode - Altitude"
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets", "assets_gen"]
        }

        androidTest {
            java.srcDirs = ["tests-ui/src"]
            res.srcDirs = ["tests-ui/res"]
            assets.srcDirs = ["tests-ui/assets"]
        }

        test {
            java.srcDirs = ["tests-jvm/src"]
        }
    }

    signingConfigs {
        stg_debug {
            keyAlias "stg_debug"
            keyPassword "stg_debug"
            storeFile file("../android-keystore/STG-debug.jks")
            storePassword "stg_debug"
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
            signingConfig signingConfigs.stg_debug

            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/maven/com.googlecode.json-simple/json-simple/pom.properties"
        exclude "META-INF/maven/com.googlecode.json-simple/json-simple/pom.xml"
    }
}

dependencies {
    /* Google Play Services */
    implementation 'com.google.android.gms:play-services-gcm:${rootProject.ext.play_services}'

    /* local dependencies */
    implementation project(':mobile:com.sap.sailing.android.shared')

    /* JVM tests */
    testImplementation 'junit:junit:${rootProject.ext.junit}'
    testImplementation 'org.mockito:mockito-core:${rootProject.ext.mockito}'
}

// Custom tasks

/**
 * Deletes apk and version from www apps directory
 */
clean.doLast {
    ant.delete() {
        fileset(dir: file("../../java/com.sap.sailing.www/apps"), includes: "$packageName*")
    }
}

/**
 * Copies apk into www apps directory and create version file
 */
build.doLast {
    // Copy APK file
    copy {
        from("build/outputs/apk") {
            include "**/*debug.apk"
        }
        into "../../java/com.sap.sailing.www/apps"
        rename { String fileName -> fileName.replace("-debug", "") }
    }

    // Create version file
    def versionFile = file("../../java/com.sap.sailing.www/apps/$packageName." + "version")
    versionFile.text = "$packageName" + ".apk=$verCode"
}
