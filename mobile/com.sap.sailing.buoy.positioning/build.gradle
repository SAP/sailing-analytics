apply plugin: "com.android.application"
apply from: "../git.gradle"

def packageName = "com.sap.sailing.android.buoy.positioning.app"
def verCode = 1

android {
    buildToolsVersion "22.0.1"
    compileSdkVersion 22

    defaultConfig {
        applicationId packageName
        minSdkVersion 14
        targetSdkVersion 22
        versionCode verCode
        versionName "$verCode - Altitude"
        useJack false // not working on Windows yet
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets"]
        }

        androidTest {
            java.srcDirs = ["tests/src"]
            res.srcDirs = ["tests/res"]
            assets.srcDirs = ["tests/assets"]
        }
    }

    signingConfigs {
        stg_debug {
            keyAlias "stg_debug"
            keyPassword "stg_debug"
            storeFile file("../android-keystore/STG-debug.jks")
            storePassword "stg_debug"
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
            signingConfig signingConfigs.stg_debug

            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/maven/com.googlecode.json-simple/json-simple/pom.properties"
        exclude "META-INF/maven/com.googlecode.json-simple/json-simple/pom.xml"
    }
}

dependencies {
    /* support libraries */
    compile "com.android.support:appcompat-v7:22.1.1"

    /* Google Play Services */
    compile "com.google.android.gms:play-services-location:6.5.87"

    /* local dependencies */
    compile project(":mobile:com.sap.sailing.android.shared")

    /* all local jar-libs */
//    compile fileTree(dir: "libs", include: ["*.jar"])
}

// Custom tasks

/**
 * Deletes apk and version from www apps directory
 */
clean.doLast {
    ant.delete() {
        fileset(dir: file("../../java/com.sap.sailing.www/apps"), includes: "$packageName*")
    }
}

/**
 * Copies apk into www apps directory and create version file
 */
build.doLast {
    // Copy APK file
    copy {
        from("build/outputs/apk") {
            include "**/*debug.apk"
        }
        into "../../java/com.sap.sailing.www/apps"
        rename { String fileName -> fileName.replace("-debug", "") }
    }

    // Create version file
    def versionFile = file("../../java/com.sap.sailing.www/apps/$packageName." + "version")
    versionFile.text = "$packageName" + ".apk=$verCode"
}
