apply plugin: "com.android.application"
apply from: "../git.gradle"

def packageName = "com.sap.sailing.racecommittee.app"
def verCode = 7

android {
    buildToolsVersion rootProject.ext.buildTools
    compileSdkVersion rootProject.ext.compileSdk

    defaultConfig {
        applicationId packageName
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode verCode
        versionName "$verCode - Laser"
        useJack false // not working on Windows yet

        /* UI testing with espresso */
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets"]
        }

        androidTest {
            java.srcDirs = ["tests-ui/src"]
            res.srcDirs = ["tests-ui/res"]
            assets.srcDirs = ["tests-ui/assets"]
        }

        test {
            java.srcDirs = ["tests-jvm/src"]
        }
    }

    signingConfigs {
        stg_debug {
            keyAlias "stg_debug"
            keyPassword "stg_debug"
            storeFile file("../android-keystore/STG-debug.jks")
            storePassword "stg_debug"
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
            signingConfig signingConfigs.stg_debug

            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude ".readme"
        exclude "pom.properties"
        exclude "pom.xml"
    }
}

dependencies {
    compile "com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.6.2"

    /* support libraries */
    compile "com.android.support:appcompat-v7:${rootProject.ext.support}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.support}"

    /* Google Play Services */
    compile "com.google.android.gms:play-services-location:${rootProject.ext.play_services}"

    /* local dependencies */
    compile project(":mobile:com.sap.sailing.android.shared")

    /* all local jar-libs */
    compile fileTree(dir: "libs", include: ["*.jar"])

    /* JVM tests */
    testCompile "junit:junit:${rootProject.ext.junit}"
    testCompile "org.mockito:mockito-core:${rootProject.ext.mockito}"

    /* UI testing with espresso */
    androidTestCompile "com.android.support:support-annotations:${rootProject.ext.support}"
    androidTestCompile "com.android.support.test:runner:${rootProject.ext.test}"
    androidTestCompile "com.android.support.test:rules:${rootProject.ext.test}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${rootProject.ext.espresso}"
    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:${rootProject.ext.espresso}") {
        exclude group: "com.android.support", module: "appcompat"
        exclude group: "com.android.support", module: "support-v4"
        exclude module: "recyclerview-v7"
    }
}

// Custom tasks

/**
 * Deletes apk and version from www apps directory
 */
clean.doLast {
    ant.delete() {
        fileset(dir: file("../../java/com.sap.sailing.www/apps"), includes: "$packageName*")
    }
}

/**
 * Copies apk into www apps directory and create version file
 */
build.doLast {
    // Copy APK file
    copy {
        from ("build/outputs/apk") {
            include "**/*debug.apk"
        }
        into "../../java/com.sap.sailing.www/apps"
        rename { String fileName -> fileName.replace("-debug", "") }
    }

    // Create version file
    def versionFile = file("../../java/com.sap.sailing.www/apps/$packageName." + "version")
    versionFile.text = "$packageName" + ".apk=$verCode"
}
