apply plugin: "com.android.application"

def packageName = "com.sap.sailing.racecommittee.app"
def verCode = 6

android {
    buildToolsVersion "21.1.2"
    compileSdkVersion 21

    defaultConfig {
        applicationId packageName
        minSdkVersion 13
        targetSdkVersion 21
        versionCode verCode
        versionName "$verCode - Laser"
        useJack true
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ['assets']
        }
    }

    signingConfigs {
        stg_debug {
            keyAlias 'stg_debug'
            keyPassword 'stg_debug'
            storeFile file('../android-keystore/STG-debug.jks')
            storePassword 'stg_debug'
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix ".debug"
            signingConfig signingConfigs.stg_debug
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt", "../proguard-mobile.txt"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude ".readme"
        exclude "pom.properties"
        exclude "pom.xml"
    }
}

dependencies {
    /* support libraries */
    compile "com.android.support:appcompat-v7:21.0.3"
    compile "com.android.support:recyclerview-v7:21.0.3"

    /* Google Play Services */
    compile 'com.google.android.gms:play-services-location:6.5.87'

    /* local dependencies */
    compile project(":mobile:com.sap.sailing.android.shared")

    /* all local jar-libs */
    compile fileTree(dir: "libs", include: ["*.jar"])
}

// Custom tasks

/**
 * Deletes apk and version from www apps directory
 */
clean.doLast {
    ant.delete() {
        fileset(dir: file("../../java/com.sap.sailing.www/apps"), includes: "$packageName*")
    }
}

/**
 * Copies apk into www apps directory and create version file
 */
build.doLast {
    // Copy APK file
    copy {
        from ("build/outputs/apk") {
            include "**/*release-unsigned.apk"
        }
        into "../../java/com.sap.sailing.www/apps"
        rename { String fileName -> fileName.replace("-release-unsigned", "") }
    }

    // Create version file
    def versionFile = file("../../java/com.sap.sailing.www/apps/$packageName." + "version")
    versionFile.text = "$packageName" + ".apk=$verCode"
}